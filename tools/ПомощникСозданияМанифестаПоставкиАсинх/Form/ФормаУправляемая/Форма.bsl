
&НаКлиенте
Процедура СгенерироватьМанифест(Команда)
	
	СформироватьФайлМанифеста();
	
КонецПроцедуры

&НаКлиенте
Функция ИмяМанифеста()

	Возврат "package.edf";

КонецФункции // ИмяМанифеста()


&НаКлиенте
Процедура СформироватьФайлМанифеста()
	
	АдресФайлаНаСервере = СформироватьМанифестНаСервере();
	Если АдресФайлаНаСервере <> Неопределено Тогда
		
		Интерактивно = ПустаяСтрока(Объект.ВыходнойФайл);
		
		Если Интерактивно Тогда
			ИмяФайла = ИмяМанифеста();
		Иначе
			ИмяФайла = Объект.ВыходнойФайл;
		КонецЕсли;
		
		Если ПолучитьФайл(АдресФайлаНаСервере, ИмяФайла, Интерактивно) Тогда
			ПоказатьОповещениеПользователя("Информация",,"Файл манифеста сохранен");	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьМанифестНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Если Не ОбработкаОбъект.ПроверитьЗаполнение() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФайла = ПолучитьИмяВременногоФайла();
	ОбработкаОбъект.СформироватьМанифест(ИмяФайла);
	
	ДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеФайла);
	
КонецФункции


&НаКлиенте
Функция ДиалогВыбораВыходногоФайла()
	
	Расширение = "edf";
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Расширение = Расширение;
	Диалог.Фильтр = СтрЗаменить("Манифест сборки релиза (*.%)|*.%", "%", Расширение);
	Если ПустаяСтрока(Объект.ВыходнойФайл) Тогда
		Диалог.ПолноеИмяФайла = ИмяМанифеста();
	Иначе
		Диалог.ПолноеИмяФайла = Объект.ВыходнойФайл;
	КонецЕсли;
	
	Возврат Диалог;
	
КонецФункции

&НаКлиенте
Процедура ВыходнойФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Диалог = ДиалогВыбораВыходногоФайла();
	Диалог.Показать(
		Новый ОписаниеОповещения(
			"ВыходнойФайлНачалоВыбораЗавершение",
			ЭтотОбъект, 
			Новый Структура("Диалог", Диалог)
		)
	);
КонецПроцедуры

&НаКлиенте
Процедура ВыходнойФайлНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		Объект.ВыходнойФайл = Диалог.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.КаталогШаблоновПоставщика = "mycompany";
КонецПроцедуры
